reset
set contour
unset surface
set cntrparam levels incr -0.3,0.1,0.5
#set isosample 100,100

set view map
set xrange [0:10]
set yrange [0:10]

set dgrid3d 100,100,4

set table "contour.txt"
splot 'tf_Rusanov_ordre_2_1.txt' u 1:2:3
unset table

unset contour
set surface
set table "dgrid.txt"
splot 'tf_Rusanov_ordre_2_1.txt' u 1:2:3
unset table

reset
set pm3d map
unset key 
#set palette defined (0 '#352a87', 1 '#0363e1',2 '#1485d4', 3 '#06a7c6', 4 '#38b99e', 5 '#92bf73', 6 '#d9ba56', 7 '#fcce2e', 8 '#f9fb0e')
set palette defined (0 '#fc0d0d', 1 '#ff6c03',2 '#02b302', 3 '#05a17c', 4 '#0522a1', 5 '#7a07e6', 6 '#e607c1', 7 '#e6074a', 8 '#91041a')
set autoscale fix
set grid

splot 'dgrid.txt' w pm3d, 'contour.txt' w l lc rgb "black"
